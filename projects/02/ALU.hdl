// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   //zero the x input
   Mux16(a=x, b=false, sel=zx, out=tmpzx);
   Not16(in=tmpzx, out=nottmpzx);

   //negate the x input
   Mux16(a=tmpzx, b=nottmpzx, sel=nx, out=tmpnx);

   //zero the y input
   Mux16(a=y, b=false, sel=zy, out=tmpzy);
   Not16(in=tmpzy, out=nottmpzy);

   //negate the y input
   Mux16(a=tmpzy, b=nottmpzy, sel=ny, out=tmpny);

   //compute the f
   And16(a=tmpnx, b=tmpny, out=andxy);
   Add16(a=tmpnx, b=tmpny, out=addxy);
   Mux16(a=andxy, b=addxy, sel=f, out=outf);

   Not16(in=outf, out=notf);

   //output
   Mux16(a=outf, b=notf, sel=no, out=out);


   //count the status
   //s is the sign
   Mux16(a=outf, b=notf, sel=no, out[15]=s, out[0..14]=t);
   Mux16(a=outf, b=notf, sel=no, out[0..7]=aa, out[8..15]=bb);

   // if a is zero
   Or8Way(in=aa, out=aiszero);

   //if b is zero
   Or8Way(in=bb, out=biszero);

   //if output is zero
   Or(a=aiszero, b=biszero, out=outzero);

   //status bit
   Mux(a = true,b = false,sel = outzero,out = zr);
   Mux(a=false, b=true, sel=s, out=ng);
}